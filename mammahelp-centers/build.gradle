import org.gradle.plugins.ide.eclipse.model.SourceFolder
import org.gradle.plugins.ide.eclipse.model.Container

apply plugin: 'eclipse'
apply plugin: 'application'

version = '0.79'
group = "cz.mammahelp.tools"
mainClassName = "cz.mammahelp.tools.MamoCenters"

repositories { mavenCentral() }

dependencies {
	compile project(':common')
	compile 'com.google.code.geocoder-java:geocoder-java:0.16'
	compile 'com.squareup.retrofit:converter-simplexml:1.6.1'
	compile 'net.sf.jtidy:jtidy:r938'
	compile 'org.slf4j:slf4j-log4j12:1.7.7'
	compile 'hsqldb:hsqldb:1.8.0.10'
	
}

configurations { provided }


eclipse.classpath.plusConfigurations += configurations.compile


eclipse {

	classpath {
		containers (
				"org.eclipse.jdt.junit.JUNIT_CONTAINER/4"
				)

		noExportConfigurations += [configurations.provided]

		file {
			beforeMerged { classpath ->
				classpath.entries.clear()
			}
			whenMerged { cp ->
				cp.entries.findAll {
					it instanceof SourceFolder && it.path.startsWith("src/main/")
				}*.output = "target/main"
				cp.entries.findAll {
					it instanceof SourceFolder && it.path.startsWith("src/test/")
				}*.output = "target/test"
				cp.entries.findAll {
					it instanceof Container && it.path == "org.eclipse.jdt.junit.JUNIT_CONTAINER/4"
				}*.exported = false
				cp.entries.removeAll { it.kind == "output" }
			}
		}
	}

	project {
		natures 'org.springsource.ide.eclipse.gradle.core.nature',
				'org.eclipse.jdt.core.javanature',
				'org.eclipse.jdt.groovy.core.groovyNature'

		buildCommand 'org.eclipse.jdt.core.javabuilder'
		name 'mammahelp-centers'
	}
}

task fatJar(type: Jar) {
	baseName = project.name + '-all'
	from {
		configurations.compile.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	with jar
}

jar {
	manifest.
			attributes ( provider: 'gradle',
			'Implementation-Vendor-Id': 'hPa',
			'Implementation-Vendor': 'Petr Vran√≠k',
			'Implementation-Version': 1.0
			)
}